cmake_minimum_required(VERSION 3.18)

find_package(GTest REQUIRED CONFIG)

file(GLOB_RECURSE UNIT_TEST_SOURCES "test_utility/*.cpp")
set(DEPENDENCIES cupoch_utility)

function(TEST TEST_NAME)
  set(DEPS)
  foreach(module ${TEST_NAME} ${ARGN})
    if(NOT BUILD_cupoch_${module})
      message(STATUS "Skipping example ${TEST_NAME} because cupoch_${module} is not enabled")
      return()
    endif()
    list(APPEND DEPS cupoch::${module})
  endforeach()
  file(GLOB_RECURSE TEST_SOURCES "${TEST_NAME}/*.cpp")
  set(UNIT_TEST_SOURCES ${UNIT_TEST_SOURCES} ${TEST_SOURCES} PARENT_SCOPE)
  set(DEPENDENCIES ${DEPENDENCIES} ${DEPS} PARENT_SCOPE)
endfunction()

TEST(collision geometry)
TEST(geometry camera)
TEST(integration io)
TEST(io)
TEST(knn geometry)
TEST(odometry geometry)
TEST(planning geometry)
TEST(registration geometry)

add_executable(unittests ${UNIT_TEST_SOURCES})
add_definitions(-DTEST_DATA_DIR="${PROJECT_SOURCE_DIR}/examples/testdata")
target_link_libraries(unittests PRIVATE ${DEPENDENCIES} GTest::GTest GTest::Main)